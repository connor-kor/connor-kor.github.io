---
title: 강의9
category: fullstack-class
---

# 11.15

**[동적 SQL] 필요성**

1. 컴파일 시에 SQL 문장이 확정이 되지 않은 경우. WHERE 조건절, SELECT 항목이 동적으로 변하는 경우
2. PL/SQL 블록 상에서 DDL 문을 실행해야 하는 경우 (CREATE, ATLER, DROP, TRUNCATE 문)
3. PL/SQL 블록 상에서 ALTER 

동적 SQL 을 사용하는 방법

1. 원시 동적 SQL (Native Dynamic SQL: NDS) +++
2. dbms_sql 패키지 사용

동적 SQL 을 실행할 때

```sql
EXECUTE 프로시저명;
```

**형식**

```sql
EXECUTE IMMEDIATE 동적SQL문
[INTO 변수...]
[USING 모드(IN OUT INOUT) 파라미터...]
```

예)

**익명프로시저**

```
20, 7369, SMITH, CLERK
```



```sql
-- ㄱ. 익명프로시저
DECLARE
    vsql VARCHAR2(1000); -- 동적 SQL 을 저장할 변수 선언
    vdeptno emp.deptno%TYPE;
    vempno emp.empno%TYPE;
    vename emp.ename%TYPE;
    Vjob emp.job%TYPE;
BEGIN
    -- 동적 SQL 작성
    vsql := 'SELECT deptno, empno, ename, job ';
    vsql := vsql || 'FROM emp ';
    vsql := vsql || 'WHERE empno = 7369';
    
    -- 동적 SQL 실행
    EXECUTE IMMEDIATE vsql
    INTO vdeptno, vempno, vename, vjob;
    
    dbms_output.put_line(vdeptno || ', ' || vempno || ', ' || vename || ', ' || vjob);
END;
```

> vsql 사이에 띄어쓰기를 꼭 할 것!!

**저장프로시저**

```sql
-- ㄴ. 저장 프로시저
CREATE OR REPLACE PROCEDURE up_nds01 (
    pempno emp.empno%TYPE
)
IS
    vsql VARCHAR2(1000); -- 동적 SQL 을 저장할 변수 선언
    vdeptno emp.deptno%TYPE;
    vempno emp.empno%TYPE;
    vename emp.ename%TYPE;
    Vjob emp.job%TYPE;
BEGIN
    -- 동적 SQL 작성
    vsql := 'SELECT deptno, empno, ename, job ';
    vsql := vsql || 'FROM emp ';
    vsql := vsql || 'WHERE empno = :pempno';
    
    -- 동적 SQL 실행
    EXECUTE IMMEDIATE vsql
    INTO vdeptno, vempno, vename, vjob
    USING pempno;
    
    dbms_output.put_line(vdeptno || ', ' || vempno || ', ' || vename || ', ' || vjob);
END;
```



**dept 테이블에 INSERT 를 수행하는 동적 SQL**

```sql
CREATE OR REPLACE PROCEDURE up_insdept (
    pdname dept.dname%TYPE
    , ploc dept.loc%TYPE
)
IS  
    vsql    VARCHAR2(1000);
    vdeptno dept.deptno%TYPE;
BEGIN
    SELECT max(deptno) + 10 INTO vdeptno
    FROM dept;
    
    vsql := 'INSERT INTO dept ';
    vsql := vsql || 'VALUES (:deptno, :dname, :loc) ';
    
    EXECUTE IMMEDIATE vsql
    USING vdeptno, pdname, ploc;
END;
```

```
10	ACCOUNTING	NEW YORK
20	RESEARCH	DALLAS
30	SALES	CHICAGO
40	OPERATIONS	BOSTON
50	QC	
```



```sql
EXEC up_insdept('A', 'B');
```

**테이블 만들기**

```sql
DECLARE 
    vsql VARCHAR2(1000);
BEGIN
    vsql := 'CREATE TABLE tbl_nds (id number, name varchar2(1000))';
    EXECUTE IMMEDIATE vsql;
END;
```

```sql
OPEN 커서명 FOR vsql USING pdeptno;
```

예제

```sql
CREATE OR REPLACE PROCEDURE up_nds02 (
    pdeptno emp.deptno%TYPE
)
IS
    vsql VARCHAR2(1000);
    vCursor SYS_REFCURSOR;
    vrow emp%ROWTYPE;
BEGIN 
    vsql := 'SELECT * ';
    vsql := vsql || 'FROM emp ';
    vsql := vsql || 'WHERE deptno = :deptno ';
    
--    EXECUTE IMMEDIATE vsql USING pdeptno; X 
    OPEN vCursor FOR vsql USING pdeptno;
    LOOP
        FETCH vCursor INTO vrow;
        EXIT WHEN vCursor%NOTFOUND;
        dbms_output.put_line(vrow.ename || ', ' || vrow.job);
    END LOOP;
    CLOSE vCursor;
END;
```

```sql
EXEC up_nds02(30);
```

